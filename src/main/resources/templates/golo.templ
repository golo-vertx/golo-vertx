@comment{"Templating! The next row outputs all interfaces annotated with VertxGen-annotation."}
@includeNamed{"common.templ"}

@comment{'the current index of the cache'}
@code{cacheDecls=new java.util.ArrayList()}

@code{
  def genGoloModuleName(type) {
    return type.raw.translatePackageName("golo");
  }
}

@declare{'invokeDelegate'}
@if{method.staticMethod}@{type.raw}@else{}@includeNamed{'rewriteDelegate';method=method}@end{}.@{method.name}(@foreach{param: method.params}@includeNamed{'paramArgument';param=param}@end{', '})
@end{}

@declare{'rewriteDelegate'}
@if{method.ownerTypes.size() > 1}
(  (@{method.ownerTypes.iterator().next().name}) this.delegate)
@else{method.ownerTypes.iterator().next() == type && concrete}
this.delegate
@else{}
((@{method.ownerTypes.iterator().next().name}) this.delegate)
@end{}
@end{}

@declare{'paramArgument'}
	@if{param.type.kind == CLASS_DATA_OBJECT}
		@{param.name} != null ? new @{param.type.name}(new io.vertx.core.json.JsonObject(@{param.name})) : null
	@else{param.type.kind.json}
		@{param.name} != null ? new @{param.type.name}(@{param.name}) : null
	@else{param.type.kind == CLASS_API}
		(@{param.type.name})@{param.name}.getDelegate()
	@else{param.type.kind == CLASS_OBJECT}
		InternalHelper.unwrapObject(@{param.name})
	@else{param.type.kind == CLASS_LIST}
		@code{listType=param.type.args[0]}
		@if{listType.kind == CLASS_API}
			(List<@{listType.name}>)(@{param.name}.stream().map((underpants) -> underpants.getDelegate()).collect(java.util.stream.Collectors.toList())
		@else{listType.kind == CLASS_JSON_OBJECT}
			@{param.name}.stream().map((underpants) -> new JsonObject(underpants)).collect(java.util.stream.Collectors.toList()
		@else{listType.kind == CLASS_JSON_ARRAY}
			@{param.name}.stream().map((underpants) -> new JsonArray(underpants)).collect(java.util.stream.Collectors.toList()
		@else{listType.kind == CLASS_DATA_OBJECT}
			@{param.name}.stream().map((underpants) -> new @{listType.simpleName}(new JsonObject(underpants))).collect(java.util.stream.Collectors.toList())
		@else{}
			@{param.name}
		@end{}
	@else{param.type.kind == CLASS_SET}
			@code{setType=param.type.args[0]}
		@if{setType.kind == CLASS_API}
			(Set<@{setType.name}>)(@{param.name}.collect({underpants -> underpants.getDelegate()}) as Set)
		@else{setType.kind == CLASS_JSON_OBJECT}
			@{param.name}.collect({underpants -> new JsonObject(underpants)}) as Set
		@else{setType.kind == CLASS_JSON_ARRAY}
			@{param.name}.collect({underpants -> new JsonArray(underpants)}) as Set
		@else{setType.kind == CLASS_DATA_OBJECT}
			@{param.name}.collect({underpants -> new @{setType.simpleName}(new JsonObject(underpants))}) as Set
		@else{}
			@{param.name}
		@end{}
	@else{param.type.kind == CLASS_MAP}
		@code{mapType=param.type.args[1]}
		@if{mapType.kind == CLASS_API}
			(Map<String, @{mapType.name}>)(@{param.name}.collectEntries({k, v -> [k, v.getDelegate()]}))
		@else{mapType.kind == CLASS_JSON_OBJECT}
			(Map<String, @{mapType.name}>)(@{param.name}.collectEntries({k, v -> [k, new JsonObject(v)]}))
		@else{mapType.kind == CLASS_JSON_ARRAY}
			(Map<String, @{mapType.name}>)(@{param.name}.collectEntries({k, v -> [k, new JsonArray(v)]}))
		@else{}
			@{param.name}
		@end{}
	@else{param.type.kind == CLASS_HANDLER}
		@code{eventType=param.type.args[0]}
		@code{eventKind=eventType.kind}
		@if{eventKind == CLASS_OTHER || eventKind.basic || eventKind == CLASS_VOID || eventKind == CLASS_THROWABLE}
			@{param.name}
		@else{eventKind == CLASS_DATA_OBJECT}
			new Handler<@{eventType.simpleName}>() {\n
			      public void handle(@{eventType.simpleName} event) {\n
			        @{param.name}.handle((Map<String, Object>)InternalHelper.wrapObject(event.toJson()));\n
			      }\n
			    }
		@else{eventKind == CLASS_JSON_OBJECT}
			new Handler<JsonObject>() {\n
			      public void handle(JsonObject event) {\n
			        @{param.name}.handle((Map<String, Object>)InternalHelper.wrapObject(event));\n
			      }\n
			    }
		@else{eventKind == CLASS_JSON_ARRAY}
			new Handler<JsonArray>() {\n
			      public void handle(JsonArray event) {\n
			        @{param.name}.handle((List<Object>)InternalHelper.wrapObject(event));\n
			      }\n
		    }
		@else{eventKind == CLASS_LIST || eventKind == CLASS_SET}
			@code{elementType=eventType.args[0]}
			@code{elementKind=elementType.kind}
			@if{elementKind == CLASS_PRIMITIVE || elementKind == CLASS_BOXED_PRIMITIVE || elementKind == CLASS_STRING}
				@{param.name}
			@else{elementKind == CLASS_API || elementKind.json || elementKind == CLASS_DATA_OBJECT}
				@code{collectionName=eventType.raw.simpleName}
				@if{elementKind == CLASS_API}
					@code{toGoloElement='' + genConstructor(elementType, "it")}
					@code{elementJavaType=elementType.name}
				@else{elementKind == CLASS_JSON_OBJECT}
					@code{toGoloElement='InternalHelper.wrapObject(it)'}
					@code{elementJavaType='JsonObject'}
				@else{elementKind == CLASS_DATA_OBJECT}
					@code{toGoloElement='(Map<String, Object>)InternalHelper.wrapObject(it.toJson())'}
					@code{elementJavaType=elementType.simpleName}
				@else{}
					@code{toGoloElement='InternalHelper.wrapObject(it)'}
					@code{elementJavaType='JsonArray'}
				@end{}
				new Handler<@{collectionName}<@{elementJavaType}>>() {\n
				      public void handle(@{collectionName}<@{elementJavaType}> event) {\n
				        @{param.name}.handle(event.collect({\n
				          @{toGoloElement}\n
				        }) as @{collectionName});\n
				      }\n
				    }
			@else{}
				null
			@end{}
		@else{eventKind == CLASS_API}
			new Handler<@{eventType.erased.name}>() {\n
			      public void handle(@{eventType.erased.name} event) {\n
			        @{param.name}.handle(@{genConstructor(eventType.raw, "event")});\n
			      }\n
			    }
		@else{eventKind == CLASS_ASYNC_RESULT}
			@code{resultType=eventType.args[0]}
			@code{resultKind=resultType.kind}
			@if{resultKind == CLASS_OTHER || resultKind.basic || resultKind == CLASS_VOID}
				@{param.name}
			@else{resultKind == CLASS_DATA_OBJECT}
				@includeNamed{'resultHandlerTemplate';eventJavaType=resultType.name;eventGoloType='Map<String, Object>';callbackObject='(Map<String, Object>)InternalHelper.wrapObject(event.result().toJson())';callbackName=param.name}
			@else{resultKind == CLASS_LIST || resultKind == CLASS_SET}
				@code{elementType=resultType.args[0]}
				@code{elementKind=elementType.kind}
				@if{elementKind == CLASS_PRIMITIVE || elementKind == CLASS_BOXED_PRIMITIVE || elementKind == CLASS_STRING}
					@{param.name}
				@else{elementKind == CLASS_API || elementKind.json || elementKind == CLASS_DATA_OBJECT}
					@code{collectionName=resultType.raw.simpleName}
					@if{elementKind == CLASS_API}
						@code{toGoloElement=genConstructor(elementType.raw, "element")}
						@code{elementJavaType=elementType.name}
						@code{elementGoloType=elementType.simpleName}
					@else{elementKind == CLASS_DATA_OBJECT}
						@code{toGoloElement='(Map<String, Object>)InternalHelper.wrapObject(element.toJson())'}
						@code{elementJavaType=elementType.simpleName}
						@code{elementGoloType='Map<String, Object>'}
					@else{elementKind == CLASS_JSON_OBJECT}
						@code{toGoloElement='InternalHelper.wrapObject(element)'}
						@code{elementJavaType='JsonObject'}
						@code{elementGoloType='Map<String, Object>'}
					@else{}
						@code{toGoloElement='InternalHelper.wrapObject(element)'}
						@code{elementJavaType='JsonArray'}
						@code{elementGoloType='List<Object>'}
					@end{}
					@code{eventJavaType=collectionName + '<' + elementJavaType + '>'}
					@code{eventGoloType=collectionName + '<' + elementGoloType + '>'}
					@code{callbackObject='event.result().stream().map((element) -> {\n            return ' + toGoloElement + ';\n          }).collect(java.util.stream.Collectors.to' + collectionName + '())'}
					@includeNamed{'resultHandlerTemplate';eventJavaType=eventJavaType;eventGoloType=eventGoloType;callbackObject=callbackObject;callbackName=param.name}
				@else{}
					null
				@end{}
			@else{resultKind == CLASS_JSON_OBJECT}
				@includeNamed{'resultHandlerTemplate';eventJavaType=resultType.name;eventGoloType='Map<String, Object>';callbackObject='(' + eventGoloType + ')InternalHelper.wrapObject(event.result())';callbackName=param.name}
			@else{resultKind == CLASS_JSON_ARRAY}
				@includeNamed{'resultHandlerTemplate';eventJavaType=resultType.name;eventGoloType='List<Object>';callbackObject='(' + eventGoloType + ')InternalHelper.wrapObject(event.result())';callbackName=param.name}
			@else{resultKind == CLASS_OBJECT}
				@includeNamed{'resultHandlerTemplate';eventJavaType='Object';eventGoloType='Object';callbackObject='InternalHelper.wrapObject(event.result())';callbackName=param.name}
			@else{resultKind == CLASS_API}
				@code{resultType=resultType.erased}
				@includeNamed{'resultHandlerTemplate';eventJavaType=resultType.name;eventGoloType=resultType.simpleName;callbackObject='new ' + resultType.simpleName + '(event.result())';callbackName=param.name}
			@else{}
				null /* Handler<AsyncResult<@{resultType.name}>> with kind @{resultKind} not yet implemented */
			@end{}
		@else{eventKind == CLASS_OBJECT}
			new Handler<Object>() {\n
			      public void handle(Object event) {\n
			        @{param.name}.handle((@{eventType.simpleName})InternalHelper.wrapObject(event));\n
			      }\n
			    }
		@else{}
			null /* Handler<@{eventType.name}> with kind @{eventKind} not yet implemented */
		@end{}
	@else{}
		@{param.name}
	@end{}
@end{}

@declare{'resultHandlerTemplate'}
	new Handler<AsyncResult<@{eventJavaType}>>() {\n
	      public void handle(AsyncResult<@{eventJavaType}> event) {\n
	        AsyncResult<@{eventGoloType}> f;\n
	        if (event.succeeded()) {\n
	          f = InternalHelper.<@{eventGoloType}>result(@{callbackObject});\n
	        } else {\n
	          f = InternalHelper.<@{eventGoloType}>failure(event.cause());\n
	        }\n
	        @{callbackName}.handle((AsyncResult)f);\n
	      }\n
	    }
@end{}

@declare{'paramTemplate'}
	@if{param.type.kind == CLASS_DATA_OBJECT}
		Map<String, Object> @{param.name}
	@else{param.type.kind == CLASS_JSON_OBJECT}
		Map<String, Object> @{param.name}
	@else{param.type.kind == CLASS_JSON_ARRAY}
		List<Object> @{param.name}
	@else{param.type.kind == CLASS_HANDLER && (param.type.args[0].kind.json || param.type.args[0].kind == CLASS_DATA_OBJECT)}
		@if{param.type.args[0].kind == CLASS_JSON_OBJECT || param.type.args[0].kind == CLASS_DATA_OBJECT}
			@code{goloJsonType='Map<String, Object>'}
		@else{}
			@code{goloJsonType='List<Object>'}
		@end{}
		Handler<@{goloJsonType}> @{param.name}
	@else{param.type.kind == CLASS_HANDLER && param.type.args[0].kind.collection && (param.type.args[0].args[0].kind.json || param.type.args[0].args[0].kind == CLASS_DATA_OBJECT)}
		@if{param.type.args[0].args[0].kind == CLASS_JSON_OBJECT || param.type.args[0].args[0].kind == CLASS_DATA_OBJECT}
			@code{goloJsonType='Map<String, Object>'}
		@else{}
			@code{goloJsonType='List<Object>'}
		@end{}
		Handler<@{param.type.args[0].raw.simpleName}<@{goloJsonType}>> @{param.name}
	@else{param.type.kind == CLASS_HANDLER && param.type.args[0].kind == CLASS_ASYNC_RESULT && (param.type.args[0].args[0].kind.json || param.type.args[0].args[0].kind == CLASS_DATA_OBJECT)}
		@if{param.type.args[0].args[0].kind == CLASS_JSON_OBJECT || param.type.args[0].args[0].kind == CLASS_DATA_OBJECT}
			@code{goloJsonType='Map<String, Object>'}
		@else{}
			@code{goloJsonType='List<Object>'}
		@end{}
		Handler<AsyncResult<@{goloJsonType}>> @{param.name}
	@else{param.type.kind == CLASS_HANDLER && param.type.args[0].kind == CLASS_ASYNC_RESULT && param.type.args[0].args[0].kind.collection && (param.type.args[0].args[0].args[0].kind.json || param.type.args[0].args[0].args[0].kind == CLASS_DATA_OBJECT)}
		@if{param.type.args[0].args[0].args[0].kind == CLASS_JSON_OBJECT || param.type.args[0].args[0].args[0].kind == CLASS_DATA_OBJECT}
			@code{goloJsonType='Map<String, Object>'}
		@else{}
			@code{goloJsonType='List<Object>'}
		@end{}
		Handler<AsyncResult<@{param.type.args[0].args[0].raw.simpleName}<@{goloJsonType}>>> @{param.name}
	@else{param.type.kind == CLASS_LIST}
		@if{param.type.args[0].kind == CLASS_JSON_OBJECT || param.type.args[0].kind == CLASS_DATA_OBJECT}
			List<Map<String, Object>> @{param.name}
		@else{param.type.args[0].kind == CLASS_JSON_ARRAY}
			List<List<Object>> @{param.name}
		@else{}
			@{param.type.simpleName} @{param.name}
		@end{}
	@else{param.type.kind == CLASS_SET}
		@if{param.type.args[0].kind == CLASS_JSON_OBJECT || param.type.args[0].kind == CLASS_DATA_OBJECT}
			Set<Map<String, Object>> @{param.name}
		@else{param.type.args[0].kind == CLASS_JSON_ARRAY}
			Set<List<Object>> @{param.name}
		@else{}
			@{param.type.simpleName} @{param.name}
		@end{}
	@else{param.type.kind == CLASS_MAP}
		@if{param.type.args[1].kind == CLASS_JSON_OBJECT}
			Map<String, Map<String, Object>> @{param.name}
		@else{param.type.args[1].kind == CLASS_JSON_ARRAY}
			Map<String, List<Object>> @{param.name}
		@else{}
			@{param.type.simpleName} @{param.name}
		@end{}
	@else{}
		@{param.type.simpleName} @{param.name}
	@end{}
@end{}

package @{genGoloModuleName(type)};\n\n

import io.vertx.lang.golo.InternalHelper;\n
import io.vertx.golo.core.metrics.Measured;\n
import java.util.Map;\n
import io.vertx.core.json.JsonObject;\n
@comment{"Generate the imports"}
@foreach{importedType:importedTypes}
	@if{importedType.kind == CLASS_API}
		@if{!ifacePackageName.equals(importedType.packageName)}
			import @{importedType.raw.translateName("golo")};\n
		@end{}
	@else{}
		@if{!importedType.packageName.equals("java.lang")}
			import @{importedType};\n
		@end{}
	@end{}
@end{}

// Type: @{type} \n
public @if{concrete}class@else{}interface@end{} @{helper.getSimpleName(ifaceFQCN)}

@if{concrete}
	@if{concreteSuperType != null}
	 extends @{concreteSuperType.simpleName}
	@end{}
@end{}

@if{abstractSuperTypes.size() > 0}
	 @if{concrete}implements@else{}extends@end{}
	@foreach{abstractSuperType: abstractSuperTypes}
	 @{abstractSuperType.simpleName}
	@end{', '}
@end{}

 {\n

@comment{'Compute the defaultDataObject map that store if we need a default data object value or not per method'}
@code{defaultDataObjectMap=new java.util.IdentityHashMap()}
@foreach{method:methods}
	@code{defaultDataObject=false}
	@if{method.params.size() > 0 && method.params[0].type.kind == CLASS_DATA_OBJECT}
		@code{defaultDataObject=true}
		@foreach{m:methods}
			@if{m.name == method.name && m.params == method.params.subList(1, method.params.size())}
				@code{defaultDataObject=false}
			@end{}
		@end{}
	@end{}
	@code{defaultDataObjectMap.put(method, defaultDataObject)}
@end{}
@comment{"TODO: handle overloaded methods with same name"}
@code{toChangeName=new java.util.HashMap()}
@foreach{method:methods}
	@code{toChange=false}
	@foreach{m:methods}
		@if{method!=m && method.name == m.name && method.params.size()==m.params.size()}
			@code{toChange=true}
		@end{}
	@end{}
	@code{toChangeName.put(method,toChange)}
@end{}
@if{concrete}
    @includeNamed{'classbody.templ';constructor=ifaceSimpleName}
@else{}
	public Object getDelegate();\n
	@foreach{method:methods}
	  @if{method.staticMethod}static @end{}@if{method.typeParams.size() > 0}<@foreach{typeVar:method.typeParams}@{typeVar}@end{}> @end{}@{method.returnType.simpleName} @{method.name}(@foreach{param: method.params}@includeNamed{'paramTemplate';param=param;defaultDataObject=defaultDataObjectMap[method]}@end{', '});\n
	@end{}
@end{}
}\n

@if{!concrete}
\n
class @{type.raw.simpleName}Impl@if{typeParams.size() > 0}<@foreach{typeParam:typeParams}@{typeParam.name}@end{','}>@end{} implements @{helper.getSimpleName(ifaceFQCN)} {\n
@includeNamed{'classbody.templ';constructor=(type.raw.simpleName + 'Impl')}
}\n
@end{}